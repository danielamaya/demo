#!/usr/bin/env perl
use Mojolicious::Lite;

get '/' => sub {
  my $c = shift;
  $c->render(template => 'index');
};

post '/' => sub {
  my $c = shift;
  my $www = $c->param('www');

  my $ua = Mojo::UserAgent->new;
  my $dom = $ua->get($www)->res->dom;

  my $links;

  # Match the dom against a regex for phone numbers
  # Regex pulled from NANP spec
  if ( $dom->all_text =~ /((?:(?:\+?1\s*(?:[.-]\s*)?)?(?:\(\s*([2-9]1[02-9]|[2-9][02-8]1|[2-9][02-8][02-9])\s*\)|([2-9]1[02-9]|[2-9][02-8]1|[2-9][02-8][02-9]))\s*(?:[.-]\s*)?)?([2-9]1[02-9]|[2-9][02-9]1|[2-9][02-9]{2})\s*(?:[.-]\s*)?([0-9]{4})(?:\s*(?:#|x\.?|ext\.?|extension)\s*(\d+))?)/g ) {
    $links->{contact}->{phone} = $1;
  }

  # Loop through links on the page grabbing email and any links matching
  # certain domains
  for my $el ($dom->find('a[href]')->each) {
    my $url = Mojo::URL->new($el->attr->{href});
    if ( $el->attr->{href} =~ /mailto/i ) {
      $links->{contact}->{email} = (split(/:/,$el->attr->{href}))[1];
    }
    if ( $url->host and $url->host =~ /(facebook|github|twitter|linkedin|stackoverflow)/i ) {
      push @{$links->{links}->{$1}}, { href => $url->to_string, text => $el->text };
    }
  }

  $c->render(json => $links);
};

app->start;
__DATA__

@@ index.html.ep
% layout 'default';
% title 'Welcome';

%= form_for '/', id => 'search' => (method => 'post') => begin
  %= label_for www => 'Please Enter a Web Address'
  %= text_field 'www'
  %= submit_button
% end
<ul id="results">
</ul>

@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title></head>
  <body><%= content %></body>
  <script src="https://ajax.googleapis.com/ajax/libs/jquery/2.1.3/jquery.min.js"></script>
  <script type="text/javascript">
    $(document).ready(function(){
      $( "#search" ).submit(function( event ) {
        event.preventDefault();
        $.post( '/', $(this).serialize(), function(data) {
          if (jQuery.isEmptyObject(data)) {
            $( "body" ).append('<br /><br /><b>No Results!</b>');
          }
          else {
            $('#results').empty();
            $.each(data.links, function(key, val){
                $('#results').append('<li><b>'+key+':</b> </li>');
                $.each(val, function(key, val){
                  $('#results').append('<a href='+val.href+'>'+val.text+'</a><br /> ');
                });
            });
            $.each(data.contact, function(key, val){
                $('#results').append('<li><b>'+key+':</b> '+val+'</li>');
            });
          }
        },
          'json'
        );
      });
    });
  </script>
</html>
